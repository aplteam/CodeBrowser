 R←Test_Misc_013(stopFlag batchFlag);⎕TRAP;filename;html;expected
⍝ Namespace without any fns/opr/vars but with empty sub namespaces.
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 R←∆Failed

 #.⎕SHADOW'Temp'
 'Temp'#.⎕NS''
 'NS1'#.Temp.⎕NS''
 'NS2'#.Temp.⎕NS''
 'NS3'#.Temp.⎕NS''

 filename←##.CodeBrowser.Run #.Temp
 →GoToTidyUp 0∊⍴filename

 expected←'!DOCTYPE' 'html' 'head' 'meta' 'meta' 'title' 'body' 'h1' 'div' 'div'
 html←#.APLTreeUtils.ReadUtf8File filename
 html←'<script>.*</script>'⎕R''⍠('DotAll' 1)('Greedy' 0)('Mode' 'D')⊣html
 html←'<style>.*</style>'⎕R''⍠('DotAll' 1)('Greedy' 0)('Mode' 'D')⊣html
 html←(0<≢¨html)/html
 html←1↓∊(⎕UCS 13),¨html
 →PassesIf expected≡{⍵/⍨'/'≠⊃¨⍵}(⊂'')~⍨{⍵↑⍨¯1+⌊/⍵⍳' >'}¨'<'#.APLTreeUtils.Split html

 R←∆OK

∆TidyUp:
 #.FilesAndDirs.DeleteFile filename
⍝Done
