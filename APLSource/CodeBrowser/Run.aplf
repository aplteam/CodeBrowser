 filename←{parms}Run namespaces;refs
⍝ Collects the code and variables in the (non-scripted) namespaces listed in `namespaces` and
⍝ compiles an HTML page with the code in it.\\
⍝ `namespaces` must be a text vector with fully qualified namespace name(s). You can use either
⍝ a space or a comma as separator.\\
⍝ `parms` is optional. If provided it must be a parameter space, typically created with `CreateParms`.\\
⍝ Returns the name of the file the HTML is written to.\\
⍝ The HTML page contains the CSS and JavaScript needed and is therefore fully self-contained.
⍝ You should not attempt to view it in an outdated browser like IE9.
 ⎕IO←1 ⋄ ⎕ML←1
 Init ⍬
 namespaces←MassageNameLists namespaces
 parms←{0<⎕NC ⍵:⍎⍵ ⋄ CreateParms''}'parms'
 parms.namespaces←namespaces
 ⎕SIGNAL/HandleParms parms namespaces
 filename←CreateHtml parms
 :If parms.viewInBrowser
     {}A.GoToWebPage filename
 :EndIf
 parms.cssfilename←{0=≢⍵:⍵ ⋄ 0 1∊⍨≡⍵:⍵ ⋄ ⊃{⍺,', ',⍵}/⍵}parms.cssfilename
⍝Done
