 R←Test_Misc_013(stopFlag batchFlag);⎕TRAP;filename;html;expected
⍝ Namespace without any fns/opr/vars but with empty sub namespaces.
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 R←T._Failed

 #.⎕SHADOW'Temp'
 'Temp'#.⎕NS''
 'NS1'#.Temp.⎕NS''
 'NS2'#.Temp.⎕NS''
 'NS3'#.Temp.⎕NS''

 filename←##.CodeBrowser.Run #.Temp
 →T.GoToTidyUp 0∊⍴filename

 expected←'!DOCTYPE' 'html' 'head' 'meta' 'meta' 'title' 'body' 'h1' 'div' 'div'
 html←APLTreeUtils2.⊃FilesAndDirs.NGET filename 1
 html←'<script>.*</script>'⎕R''⍠('DotAll' 1)('Greedy' 0)('Mode' 'D')⊣html
 html←'<style>.*</style>'⎕R''⍠('DotAll' 1)('Greedy' 0)('Mode' 'D')⊣html
 html←(0<≢¨html)/html
 html←1↓∊(⎕UCS 13),¨html
 →T.PassesIf expected ∆LazyMatch {⍵/⍨'/'≠⊃¨⍵}(⊂'')~⍨{⍵↑⍨¯1+⌊/⍵⍳' >'}¨'<'APLTreeUtils2.Split html

 R←T._OK

∆TidyUp:
 Assert 1=FilesAndDirs.DeleteFile filename
⍝Done
