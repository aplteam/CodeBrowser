 R←Test_Misc_001(stopFlag batchFlag);⎕TRAP;filename;html;names;nav;expected;ind
⍝ Single namespace with defaults.
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 R←T._Failed

 filename←##.CodeBrowser.Run ##.TestData.NS1
 →T.GoToTidyUp 0∊⍴filename

 html←APLTreeUtils2.⊃FilesAndDirs.NGET filename 1
 names←GetFnsAndOprNamesFromHTML html
 →T.GoToTidyUp~names Matches' '~¨⍨⊃,/{↓⍵.⎕NL 3 4}¨{⍵[;2]}{2=⍴⍴r←↑⍵:r ⋄.}##.CodeBrowser.GetSubNamespaces ##.TestData.NS1
 html←⊃,/(⎕UCS 13),¨html
 nav←html{⍵[2]↑⍵[1]↓⍺}⊃'<nav id="main">.*</nav>'⎕S 0 1⍠('Greedy' 0)('Mode' 'M')('DotAll' 1)⊣html
 nav←(⎕UCS 13)APLTreeUtils2.Split nav
 expected←⊂'<a href="#NS1">'
 expected,←⊂'<a href="#NS1.NS1_a">'
 expected,←⊂'<a href="#NS1.NS1_b">'
 →T.GoToTidyUp~∧/∨/¨expected⍷¨⊂∊nav
 →T.GoToTidyUp 2≠+/'<ol'⍷∊nav
 ind←'<nav class="sub">.*</nav>'⎕S 0 1⍠('Greedy' 0)('Mode' 'M')('DotAll' 1)⊣html
 nav←{(⎕UCS 13)APLTreeUtils2.Split ⍵}¨ind{⍺[2]↑⍺[1]↓⍵}¨⊂html

 R←T._OK

∆TidyUp:
 Assert 1=FilesAndDirs.DeleteFile filename
⍝Done
