 R←Test_Parms_018(stopFlag batchFlag);⎕TRAP;html;parms;filename2;filename1
⍝ Check the `processOperators` parameter
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 R←T._Failed

 #.⎕SHADOW'Temp'
 'Temp'#.⎕NS''
 'NS1'#.Temp.⎕NS''
 #.Temp.NS1.⎕FX'r←Hello' 'r←''World'''
 #.Temp.NS1.⎕FX'r←(fns Do) arg' 'r←''Hello Universe'''
 filename1←##.CodeBrowser.Run'#.Temp.NS1'
 →T.GoToTidyUp 0∊⍴filename1
 html←⊃FilesAndDirs.NGET filename1
 →T.GoToTidyUp 1≠+/'''World'''⍷html
 →T.GoToTidyUp 1≠+/'''Hello Universe'''⍷html

 parms←##.CodeBrowser.CreateParms ⍬
 parms.processOperators←0
 filename2←parms ##.CodeBrowser.Run'#.Temp.NS1'
 →T.GoToTidyUp 0∊⍴filename2
 html←⊃FilesAndDirs.NGET filename2
 →T.GoToTidyUp 1≠+/'''World'''⍷html
 →T.GoToTidyUp 0≠+/'''Hello Universe'''⍷html

 R←T._OK

∆TidyUp:
 Assert 1 1≡FilesAndDirs.DeleteFile filename1 filename2
⍝Done
