 R←Test_Parms_016(stopFlag batchFlag);⎕TRAP;html;filename1;parms;filename2
⍝ Check the `recursive` parameter
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 R←∆Failed

 #.⎕SHADOW'Temp'
 'Temp'#.⎕NS''
 'NS1'#.Temp.⎕NS''
 #.Temp.NS1.⎕FX'r←Hello' 'r←''World'''
 'NS2'#.Temp.NS1.⎕NS''
 #.Temp.NS1.NS2.⎕FX'r←Hello2' 'r←''Universe'''
 filename1←##.CodeBrowser.Run'#.Temp.NS1'
 →GoToTidyUp 0∊⍴filename1
 html←'flat'#.APLTreeUtils.ReadUtf8File filename1
 →GoToTidyUp 1≠+/'''World'''⍷html
 →GoToTidyUp 1≠+/'''Universe'''⍷html

 parms←##.CodeBrowser.CreateParms ⍬
 parms.recursive←0
 filename2←parms ##.CodeBrowser.Run'#.Temp.NS1'
 →GoToTidyUp 0∊⍴filename2
 html←'flat'#.APLTreeUtils.ReadUtf8File filename2
 →GoToTidyUp 1≠+/'''World'''⍷html
 →GoToTidyUp 0≠+/'''Universe'''⍷html

 R←∆OK

∆TidyUp:
 :Trap 0 ⋄ #.FilesAndDirs.DeleteFile filename1 filename2 ⋄ :EndTrap
⍝Done
