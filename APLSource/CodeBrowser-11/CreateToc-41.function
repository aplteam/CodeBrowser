 lists←level CreateToc(ref parms collect);lists;width;caption;flag;html;buff
 lists←⎕NS''                ⍝ All lists: functions, operators, non-scripted namespaces (containers)
 lists.(fns opr scripts variables containers)←⊂''
 caption←(FormatLevel level),MakeBreadCrumb collect.remove RemoveLevels⍕ref
 html←⊂caption{'<h3 id="',(RemoveFirstLevel ⍵),'">',⍺,'</h3>'}⍕ref
 flag←1
 :If 0<⊃⍴↓ref.⎕NL 2 3 4 9
     width←⍕6⌈⌈0.8×⌈/⊃∘⍴¨↓ref.⎕NL 3 4 9
     (buff flag)←HandleContainers ref width flag lists parms collect
     html,←buff
     :If parms.objects[1]
     :AndIf ~0∊⍴lists.fns←' '~¨⍨↓ref.⎕NL 3
         (html flag)←html OpenNavSub flag
         html,←CreateToc_ ref parms lists.fns'Function'collect width
     :EndIf
     :If parms.objects[2]
     :AndIf ~0∊⍴lists.opr←' '~¨⍨↓ref.⎕NL 4
         (html flag)←html OpenNavSub flag
         html,←CreateToc_ ref parms lists.opr'Operator'collect width
     :EndIf
     :If parms.objects[3]
     :AndIf ~0∊⍴lists.scripts←ListScriptsIn ref
         (html flag)←html OpenNavSub flag
         html,←CreateToc_ ref parms lists.scripts'Script'collect width
     :EndIf
     :If parms.objects[4]
     :AndIf ~0∊⍴lists.variables←' '~¨⍨↓ref.⎕NL 2
         (html flag)←html OpenNavSub flag
         html,←CreateToc_ ref parms lists.variables'Variable'collect width
     :EndIf
     html,←(~flag)/⊂'</nav>'
 :EndIf
 {}WriteHTML html
⍝Done
