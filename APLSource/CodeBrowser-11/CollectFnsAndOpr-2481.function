 r←level CollectFnsAndOpr(ref name parms collect i);body;no;caption
 r←''
 :If ¯3≠≡body←(⎕UCS 13)A.Split ref.⎕VR name
     :Trap 11
         body←(,¨'<>&')⎕R'\&lt;' '\&gt;' '\&amp;'⍠('Greedy' 0)⊣body
     :Else
         body←(,¨'<>&')⎕R'\&lt;' '\&gt;' '\&amp;'⍠('Greedy' 0)⊣↓⎕FMT body
     :EndTrap
     r,←⊂'<div class="code_block" ',(collect MakeID(⍕ref),'.',name),'>'
     caption←MakeBreadCrumb RemoveFirstLevel collect.remove RemoveLevels⍕ref
     r,←⊂'<p class="caption">',(⍕i),'. ',caption,'.',name,'</p>'
     (1⊃body)←'<pre><code>',1⊃body
     r,←body,⊂'</code></pre>'
     :If parms.timestampAndAuthor
         r,←⊂AddTimestampAndAuthor ref name
     :EndIf
     r,←⊂'</div>'
 :EndIf
⍝Done
