 filename←CreateHtml parms;ns;collect;WriteHTML
 ns←⎕NS''
 ns.html←''
 WriteHTML←parms.tno∘{0∊⍴⍵:r←⍬ ⋄ 'append'A.WriteUtf8File ⍺(,A.Nest,⍵)}
 WriteHTML WriteHeader parms
 WriteHTML⊂'<h1>',(ExchangeHtmlSpecialChars parms.caption),'</h1>'
 :If ~0∊⍴parms.info
     :If 0 1∊⍨≡parms.info
     :AndIf ~(⎕UCS 13)∊parms.info
         WriteHTML(⊂'<div id="info">'),({'<p>',⍵,'</p>'}¨A.Nest ExchangeHtmlSpecialChars parms.info),⊂'</div>'
     :Else
         parms.info←ExchangeHtmlSpecialChars¨(⎕UCS 13)A.Split⍣((⎕UCS 13)∊parms.info)⊣parms.info
         WriteHTML(⊂'<div id="info">'),({({'<p>',⍵,'</p>'}⍵)}¨parms.info),⊂'</div>'
     :EndIf
 :EndIf
 collect←⎕NS''
 collect.namespaces←⍬
 collect.remove←⌊/{+/'.'=⍵}¨namespaces
 :If parms.recursive
     collect.hasMainMenu←AddMainMenu parms collect
 :Else
     collect.hasMainMenu←0
 :EndIf
 1 CollectAplObjects parms collect
 WriteHTML AddFooter parms
 WriteHTML AddParameters parms
 WriteHTML('</body>' '</html>')
 filename←parms.filename
 ⎕NUNTIE parms.tno
⍝Done
